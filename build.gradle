apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'


project.version = '3.2.10'
project.group = 'de.hhu.stups'
project.archivesBaseName = "sablecc"

allprojects {
    sourceCompatibility = 1.5
    targetCompatibility = 1.5
}

dependencies {
 testCompile 'junit:junit:4.8.2'
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    sign configurations.archives
}

jar {
	manifest {
		attributes 'Main-Class': 'org.sablecc.sablecc.SableCC'
	}
}

artifacts {
    archives javadocJar, sourcesJar
}

task deploy(dependsOn: [jar,test,javadoc], group: 'Build')

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'SableCC - Stups fork'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'This version of SableCC enriches the abstract syntax tree with information about tokens.'
        url 'https://github.com/bendisposto/sablecc-stups'

        licenses {
          license {
            name 'GNU Lesser General Public License, Version 2.1'
            url 'http://www.gnu.org/licenses/lgpl-2.1.html'
          }
        }

        scm {
          connection 'scm:git:git://github.com/bendisposto/sablecc-stups.git'
          developerConnection 'scm:git:git@github.com:bendisposto/sablecc-stups.git'
          url 'https://github.com/bendisposto/sablecc-stups'
        }


        developers {
          developer {
            id 'bendisposto'
            name 'Jens Bendisposto'
            email 'jens@bendisposto.de'
          }
        }
      }
    }
  }
}
